<!DOCTYPE HTML>
<html lang="en">
<head>
    <title>Hello Mesh </title>
    <meta charset="utf-8">
</head>

<script id="skybox-shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertex;
    attribute vec3 aNormal;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;

    varying vec3 R;

    void main() {
        R = normalize(aNormal);
        R.x = -R.x;
        gl_Position = uPMatrix*uMVMatrix*vec4(aVertex, 1.0);
    }
</script>

<script id="skybox-shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    uniform samplerCube cubeMap;

    varying vec3 R;

    void main() {
        gl_FragColor = textureCube(cubeMap, normalize(R));
    }
</script>

<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexNormal;
    attribute vec3 aVertexPosition;

    uniform mat4 uMMatrix;
    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;
    uniform mat3 uWNMatrix;

    uniform vec3 worldEye;
    
    varying vec3 vNormal;
    varying vec3 vPosition;
    varying vec3 R;
    varying vec3 T;

    void main(void) {
        // Get the vertex position in eye coordinates
        vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
        vPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;

        // Calculate the normal (n)
        vNormal = normalize(uNMatrix * aVertexNormal);

        // Get vertex position in world coordinates
        vec4 vertexPositionWorld4 = uMMatrix * vec4(aVertexPosition, 1.0);
        vec3 vWorldPosition = vertexPositionWorld4.xyz / vertexPositionWorld4.w;
        // Get normal in world coordinates
        vec3 vWorldNormal = normalize(uWNMatrix * aVertexNormal);

        vec3 eyeToSurfaceDir = normalize(vWorldPosition - worldEye);
        // Calculate reflect vector (r)
        R = reflect(-eyeToSurfaceDir, vWorldNormal);
        R.yz = -R.yz;

        // Calculate refract vector (t)
        T = refract(eyeToSurfaceDir, vWorldNormal, 1.00 / 1.52);
        T.xy = -T.xy;

        gl_Position = uPMatrix*uMVMatrix*vec4(aVertexPosition, 1.0);
    }
</script>

<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPosition;
    uniform vec3 uAmbientLightColor;
    uniform vec3 uDiffuseLightColor;
    uniform vec3 uSpecularLightColor;

    uniform vec3 uKAmbient;
    uniform vec3 uKDiffuse;
    uniform vec3 uKSpecular;
    uniform float uShininess;

    uniform samplerCube cubeMap;

    uniform int type;

    varying vec3 vNormal;
    varying vec3 vPosition;
    varying vec3 R;
    varying vec3 T;

    void main(void) {
        if (type == 0) {
            // Phong

            // Calculate the vector (l) to the light source
            vec3 vectorToLightSource = normalize(uLightPosition - vPosition);

            // Calculate n dot l for diffuse lighting
            float diffuseLightWeightning = max(dot(vNormal,
                                        vectorToLightSource), 0.0);

            // Calculate the reflection vector (r) that is needed for specular light
            vec3 reflectionVector = normalize(reflect(-vectorToLightSource,
                                                vNormal));

            // The camera in eye coordinates is located in the origin and is pointing
            // along the negative z-axis. Calculate viewVector (v)
            // in eye coordinates as:
            // (0.0, 0.0, 0.0) - vertexPositionEye3
            vec3 viewVectorEye = -normalize(vPosition);

            vec3 halfwayVector = normalize(vectorToLightSource + viewVectorEye);
            float rdotv = max(dot(vNormal, halfwayVector), 0.0);

            float specularLightWeightning = pow(rdotv, uShininess);

            // Sum up all three reflection components and send to the fragment shader
            gl_FragColor = vec4(((uAmbientLightColor*uKAmbient)
                            + (uDiffuseLightColor*uKDiffuse) * diffuseLightWeightning
                            + ((uSpecularLightColor*uKSpecular) * specularLightWeightning)), 1.0);
        } else if (type == 1) {
            // Reflection
            gl_FragColor = textureCube(cubeMap, R);
        } else {
            // Refraction
            vec4 refractColor = textureCube(cubeMap, T);
            gl_FragColor = mix(refractColor, vec4(1.0, 1.0, 1.0, 1.0), 0.3);
        }
    }
</script>

<script src="gl-matrix.js"></script>
<script src="webgl-utils.js"></script>
<script src="TriMesh.js"></script>
<script src="HelloMesh.js"></script>
<body onload="startup();">
<canvas id="myGLCanvas" width="800" height="800"></canvas>
<form id="input_form">
    <fieldset>
        <legend>Rendering Parameters</legend>
        <input type="radio" name="primitive" id="wireframe" value="wireframe"> Wireframe
        <input type="radio" name="primitive" id="polygon" value="polygon" checked> Polygon
        <input type="radio" name="primitive" id="wirepoly" value="wirepoly"> Polygon with Edges
    </fieldset>
    <fieldset>
        <legend>Rendering Types</legend>
        <input type="radio" name="type" id="Phong" value="Phong" checked> Phong
        <input type="radio" name="type" id="Reflection" value="Reflection"> Reflection
        <input type="radio" name="type" id="Refraction" value="Refraction"> Refraction
    </fieldset>
    <fieldset>
        <legend>Mesh Pose and Camera Position</legend>
        <em>Object X:</em>
        <input type="number" id="oX" value="0"> Keys W and S <br><br>
        <em>Object Y:</em>
        <input type="number" id="oY" value="0"> Keys A and D <br><br>
        <em>You can also use mouse to drag the teapot</em> <br><br>
        <em>Eye euler Y:</em>
        <input type="number" id="eY" value="0"> Keys ArrowRight and ArrowLeft <br><br>
        <em>Eye Z position:</em>
        <input type="number" id="eZ" value="0"> Keys ArrowUp and ArrowDown
    </fieldset>
</form>
</body>

</html>
